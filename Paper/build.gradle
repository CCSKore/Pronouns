plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id "io.papermc.hangar-publish-plugin" version "0.1.0"
}

group = 'net.kore'
version = '0.1.0'

shadowJar {
    archiveBaseName.set('Pronouns-Paper')
    archiveClassifier.set('')
    archiveVersion.set(version)
    destinationDirectory.set(new File(rootDir, "output"))
}

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }

    maven {
        url = "https://oss.sonatype.org/content/groups/public/"
    }

    maven {
        url = "https://repo.extendedclip.com/content/repositories/placeholderapi/"
    }
}

dependencies {
    implementation project(path: ':API')
    compileOnly "io.papermc.paper:paper-api:1.20.1-R0.1-SNAPSHOT"
    compileOnly "me.clip:placeholderapi:2.11.5"
    implementation "org.spongepowered:configurate-hocon:4.1.2"
}

hangarPublish {
    publications.register("plugin") {
        version.set(project.version as String)
        channel.set("Snapshot")
        id.set("Pronouns")
        apiKey.set(System.getenv("HANGAR_API_TOKEN"))
        platforms {
            register(Platforms.PAPER) {
                jar.set(tasks.shadowJar.flatMap { it.archiveFile })
                platformVersions.set("1.20.x")
                dependencies {
                    hangar("PlaceholderAPI") {
                        required.set(false)
                    }
                }
            }
        }
    }
}